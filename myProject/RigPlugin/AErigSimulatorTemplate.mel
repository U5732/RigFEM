//
//  Procedure Name:
//	AEmatlab
//
//  Input Value:
//	nodeName 
//
//  Output Value:
//	None
// 
global proc initSimulator(string $nodeName)
{	
	// execute
	rigSimulate -init -name $nodeName;
}
// type: simulation type 
// 0		dynamic simulation
// 1		static simulation
global proc execSimulator(string $nodeName, int $type)
{	
	int  $length = `intSliderGrp   -query -value simLengthSlider`;
	
	// init progress bar
	global string $gMainProgressBar;
    progressBar -edit
        -beginProgress
        -isInterruptable true
        -status "Simulating... "
        -maxValue $length
        $gMainProgressBar;
		
	int  $begTime = `currentTime -q`;
	int  $t;
	for ($t = $begTime; $t < $begTime + $length; $t++)
	{	
		// step progress bar
        if(`progressBar -query -isCancelled $gMainProgressBar`)
            break;
		print("\n\ncurrent Frame:"+ $t + "\n");
        progressBar -edit -step 1 $gMainProgressBar;
			
		currentTime $t;
		int $res;
		switch($type)
		{
		case 0:
			$res = `rigSimulate -step -name $nodeName`;
			break;
		case 1:
			$res = `rigSimulate -stepStatic -name $nodeName`;
			break;
		}
		if($res == 0)
			break;
	}
	
	// delete progress bar
    progressBar -edit  -endProgress $gMainProgressBar;
}
global proc saveSimulationData(string $nodeName)
{
	string $filePath = `getAttr ($nodeName+".resultPath")`;
	//`fileDialog -m 1`;
	int $res = `rigSimulate -name $nodeName -save $filePath`;
}

global proc initSimulatorControl(string $attrName)
{
	string $nodeName = `match "[^\.]*" $attrName`;	
	setUITemplate -pst "attributeEditorTemplate";
		button -label "Initialize" -c ("initSimulator " + $nodeName) initButton;
		
		intSliderGrp 	-label "Simulation Length" -field true
						-minValue 1 -maxValue 100
						-fieldMinValue  1 -fieldMaxValue  10000
						-value 10
						simLengthSlider;
		button -label "Simulate" -c ("execSimulator " + $nodeName + " 0") simButton;
		button -label "Static Simulate" -c ("execSimulator " + $nodeName + " 1") staticSimButton;
		button -label "Save Simulation Result" -c ("saveSimulationData " + $nodeName) saveButton;
	setUITemplate -ppt;
}

global proc modifySimulatorControl(string $attrName)
{
	string $nodeName = `match "[^\.]*" $attrName`;	
	button -e -c ("initSimulator " + $nodeName) initButton;	
	button -e -c ("execSimulator " + $nodeName + " 0") simButton;
	button -e -c ("execSimulator " + $nodeName + " 1") staticSimButton;
	button -e -c ("saveSimulationData " + $nodeName) saveButton;
}

global proc AErigSimulatorTemplate(string $nodeName)
{
	editorTemplate -beginScrollLayout;
		editorTemplate -beginLayout "Tet Mesh Parameters" -collapse false;
			editorTemplate -addControl "tetEdgeRatio";
			editorTemplate -addControl "tetMaxVolume";
			editorTemplate -addSeparator;
			editorTemplate -addControl "displayFemMesh";
			editorTemplate -label "Display Data Source" -addControl "displayType";
			editorTemplate -label "Ext. Force Display Length" -addControl "fieldForceShowLength";
			editorTemplate -addControl "meshTransform";
		editorTemplate -endLayout;
		
		editorTemplate -beginLayout "FEM Parameters" -collapse false;
			editorTemplate -addControl "youngModulus";
			editorTemplate -addControl "nu";
			editorTemplate -addControl "density";
		editorTemplate -endLayout;
		
		editorTemplate -beginLayout "Solver Parameters" -collapse false;
			editorTemplate -addControl "stepTime";
			editorTemplate -addControl "derivativeStep";
			
			editorTemplate -addSeparator;
			editorTemplate -addControl "inverseGradTolerance";
			editorTemplate -label "inverse Newton Step Tolerance" -addControl "inverseStepTolerance";
			editorTemplate -label "inverse CG. Step Tolerance" -addControl "inverseStepToleranceOfCG";
			editorTemplate -label "max Newton Iteration" -addControl "maxIteration";
			editorTemplate -label "max CG. Iteration" -addControl "maxIterationOfCG";			
			editorTemplate -addControl "maxParamStepEachIteration";
			
			editorTemplate -addSeparator;
			editorTemplate -addControl "savePath";
			editorTemplate -addControl "simulationType";
			editorTemplate -addControl "weightPath";
			editorTemplate -addControl "resultPath";
			editorTemplate -callCustom "initSimulatorControl" "modifySimulatorControl" "noAttr";	
		editorTemplate -endLayout;
		
		editorTemplate -beginLayout "Rig Parameter" -collapse true;
			editorTemplate -addControl "rigInitParameter";
			editorTemplate -addControl "inputForce";
		editorTemplate -endLayout;
				
		editorTemplate -beginLayout "Rig Control" -collapse true;
			editorTemplate -addControl "controlType";
			editorTemplate -addControl "targetParam";
			editorTemplate -addControl "proportionalGain";
			editorTemplate -addControl "derivativeGainRatio";
		editorTemplate -endLayout;
		
		editorTemplate -suppress "fieldData";
		editorTemplate -suppress "riggedMesh";
		
		editorTemplate -beginLayout (uiRes("m_AElocatorTemplate.kLocatorAttributes")) -collapse 0;
			AElocatorCommon $nodeName;
		editorTemplate -endLayout;

		AEgeometryShapeTemplate $nodeName;

		editorTemplate -addExtraControls;	
	editorTemplate -endScrollLayout;
}